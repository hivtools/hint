buildscript {
    ext.kotlin_version = '1.6.10'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.19.0'
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.3' apply false
    id 'io.gitlab.arturbosch.detekt' version "1.19.0"
}

subprojects {

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: "kotlin-spring"
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        config = files("$rootDir/config/detekt/detekt.yml")
        baseline = file("$rootDir/config/detekt/baseline.xml")
        buildUponDefaultConfig = true
        autoCorrect = true
        reports {
            // these reports are generated in the build/reports/detekt directory
            txt.required.set(true)
        }
    }

    tasks.detekt.jvmTarget = JavaVersion.VERSION_11

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.3'
    }

    jacoco {
        toolVersion = "0.8.8"
        reportsDirectory.set(layout.buildDirectory.dir("$projectDir/coverage"))
    }

    tasks.jacocoTestReport {
        reports {
            xml.required.set(true)
            csv.required.set(false)
        }
    }

    jar {
        enabled true
    }

    tasks.test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
        if (project.hasProperty('excludeADR')) {
            exclude "org/imperial/mrc/hint/integration/adr/**"
        }
    }

    group = 'org.imperial.mrc'
    sourceCompatibility = JavaVersion.VERSION_11
}
